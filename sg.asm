	ORG	100H
	JMP	START
BOOTSC:	EQU	400H
MBUF:	EQU	480H
OPENF:	MVI	C,15
	JMP	5
SREAD:	MVI	C,20
	JMP	5
SETDMA:	MVI	C,26
	JMP	5
STR0:	DB 10,7,'Отсутствует входной файл !$'
STR3:	DB 10,7,'Неверный адрес !$'
STR4:	DB 10,7,'Диск назначения только А или В !$'
STR5:	DB 10,7,'Диск отсутствует !$'
STR7:	DB 10,'Готовы ?$'
STR8:	DB 10,'OK.$'
STRA:	DB 'System Generator для РДС,V1.00, Copyright (c)'
	DB ' 1996 by Вьюнов В.А..$'
START:	LXI	SP,0
	LXI	D,STRA
	MVI	C,9
	CALL	5
	CALL	SETBIO
	LDA	5DH
	CPI	20H
	LXI	D,STR0
	JZ	EXITMS
	LDA	6DH
	CPI	20H
	LXI	D,STR3
	JZ	EXITMS
	LDA	6CH
	DCR	A
	STA	DISK
	LXI	D,STR4
	CPI	2
	JNC	EXITMS
	LXI	D,6DH
	CALL	GETADR
	LXI	D,STR3
	JC	EXITMS
	SHLD	BOOTSC
	SHLD	BOOTSC+2
	CALL	CLEAR
	LXI	D,5CH
	CALL	OPENF
	INR	A
	LXI	D,STR0
	JZ	EXITMS
	LXI	D,MBUF
	LXI	H,0
	SHLD	KSECT
BEGIN:	PUSH	D
	CALL	SETDMA
	LXI	D,5CH
	CALL	SREAD
	POP	D
	LHLD	KSECT
	INX	H
	SHLD	KSECT
	LXI	H,128
	DAD	D
	XCHG
	ANA	A
	JZ	BEGIN
	LHLD	KSECT
	DCX	H
	SHLD	KSECT
	LXI	B,3
BEG02:	MOV	A,H
	ANA	A
	RAR
	MOV	H,A
	MOV	A,L
	RAR
	MOV	L,A
	MOV	A,B
	RAL
	MOV	B,A
	DCR	C
	JNZ	BEG02
	MOV	A,B
	ANA	A
	JZ	$+4
	INR	L
	MOV	A,L
	STA	BOOTSC+4
	LDA	DISK
	MOV	C,A
	CALL	SELDSK
	MOV	A,H
	ORA	L
	LXI	D,STR5
	JZ	EXITMS
	LXI	D,10
	DAD	D
	MOV	E,M
	INX	H
	MOV	D,M
	XCHG
	LXI	D,BOOTSC+16
	MVI	B,15
	CALL	COPY
	LXI	H,BOOTSC
	MVI	B,31
	MVI	A,66H
BEG12:	ADD	M
	INX	H
	DCR	B
	JNZ	BEG12
	MOV	M,A
	LXI	D,STR7
	MVI	C,9
	CALL	5
	JMP	BEG10
SETBIO:	LHLD	1
	LXI	D,6
	DAD	D
	SHLD	KEY+1
	LXI	D,18
	DAD	D
	SHLD	SELDSK+1
	LXI	D,3
	DAD	D
	SHLD	SETTRC+1
	DAD	D
	SHLD	SETSEC+1
	DAD	D
	SHLD	STDMA+1
	DAD	D
	DAD	D
	SHLD	WRITE+1
	RET
BEG10:	CALL	KEY
	LXI	H,100H
	SHLD	TRACK
	LXI	H,BOOTSC
	SHLD	DMA
	LHLD	KSECT
SAVE:	PUSH	H
	CALL	SAVE10
	MVI	C,0
	CALL	WRITE
	POP	H
	DCX	H
	MOV	A,H
	ORA	L
	JNZ	SAVE
	CALL	SAVE10
	MVI	C,1
	CALL	WRITE
	LDA	4
	ANI	15
	MOV	C,A
	CALL	SELDSK
	LXI	D,STR8
	JMP	EXITMS
SAVE10:	LHLD	DMA
	MOV	B,H
	MOV	C,L
	LXI	D,128
	DAD	D
	SHLD	DMA
	CALL	STDMA
	LXI	H,SECT
	MOV	A,M
	MOV	C,A
	INR	M
	CPI	41
	JC	SAVE12
	MVI	C,1
	MVI	M,2
	LXI	H,TRACK
	INR	M
SAVE12:	CALL	SETSEC
	LXI	H,TRACK
	MOV	C,M
	CALL	SETTRC
	RET
DMA:	DW	0
TRACK:	DB	0
SECT:	DB	0
KEY:	JMP	0
SELDSK:	JMP	0
SETTRC:	JMP	0
SETSEC:	JMP	0
STDMA:	JMP	0
WRITE:	JMP	0
GETADR:	LXI	H,0
GETA00:	LDAX	D
	INX	D
	CPI	20H
	RZ
	SUI	30H
	JM	GETA10
	CPI	10
	JC	GETA02
	SUI	7
	CPI	10
	JC	GETA10
	CPI	16
	JNC	GETA10
GETA02:	MOV	C,A
	MVI	B,0
	DAD	H
	DAD	H
	DAD	H
	DAD	H
	JC	GETA10
	DAD	B
	JMP	GETA00
GETA10:	STC
	RET
COPY:	MOV	A,M
	STAX	D
	INX	H
	INX	D
	DCR	B
	JNZ	COPY
	RET
EXITMS:	MVI	C,9
	LXI	SP,0
	CALL	5
	JMP	0
CLEAR:	LXI	H,68H
	LXI	B,1800H
FILL:	MOV	M,C
	INX	H
	DCR	B
	JNZ	FILL
	RET
DISK:	DB	0
KSECT:	DB	0